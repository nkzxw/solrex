; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=Poderosa
AppVerName=Poderosa 3.0.0
AppPublisher=Poderosa Project
AppPublisherURL=http://www.poderosa.org/
AppSupportURL=http://www.poderosa.org/
AppUpdatesURL=http://www.poderosa.org/
DefaultDirName={pf}\Poderosa
DefaultGroupName=Poderosa
OutputBaseFilename=poderosa300
Compression=lzma
SolidCompression=yes
LanguageDetectionMethod=locale
ShowLanguageDialog=auto
ChangesAssociations=yes

[Tasks]
Name: desktopicon; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: associate; Description: "{cm:AssocFileExtension,Poderosa,.gts}"; Flags: unchecked

[Files]
Source: "C:\Poderosa\src\Container\bin\Release\Poderosa.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Container\bin\Release\Poderosa.pdb"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Container\bin\Release\Poderosa.exe.manifest"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Container\bin\Release\Granados.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Container\bin\Release\Poderosa.Common.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Container\bin\Release\Poderosa.Common.pdb"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Container\bin\Release\Poderosa.Terminal.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Container\bin\Release\Poderosa.Terminal.pdb"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Portforwarding\bin\Release\Portforwarding.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Portforwarding\bin\Release\Portforwarding.pdb"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Portforwarding\bin\Release\Portforwarding.exe.manifest"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Poderosa\src\Container\bin\Release\CygTerm\*"; DestDir: "{app}\Cygterm"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Poderosa\macroreference\macrosample\*"; DestDir: "{app}\macrosample"; Flags: ignoreversion
Source: "C:\Poderosa\macroreference\en\msdn\PoderosaMacro_EN.chm"; DestDir: "{app}\doc"; Flags: ignoreversion
Source: "C:\Poderosa\macroreference\ja\msdn\PoderosaMacro_JA.chm"; DestDir: "{app}\doc"; Flags: ignoreversion
Source: "C:\Poderosa\doc\LICENSE.txt"; DestDir: "{app}\doc"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Languages]
Name: "en"; LicenseFile: "C:\Poderosa\doc\LICENSE.txt"; MessagesFile: "compiler:Default.isl";
Name: "ja"; LicenseFile: "C:\Poderosa\doc\LICENSE.txt"; MessagesFile: "compiler:Languages\Japanese.isl"

[Icons]
Name: "{group}\Poderosa"; Filename: "{app}\Poderosa.exe"
Name: "{group}\Portforwarding"; Filename: "{app}\Portforwarding.exe"
Name: "{group}\Macro Reference(English)"; Filename: "{app}\doc\PoderosaMacro_EN.chm"
Name: "{group}\Macro Reference(Japanese)"; Filename: "{app}\doc\PoderosaMacro_JA.chm"
Name: "{userdesktop}\Poderosa"; Filename: "{app}\Poderosa.exe"; Tasks: desktopicon

[Registry]
;Poderosa Terminal Shortcut
Root: HKCR; Subkey: ".gts"; ValueType: string; ValueName: ""; ValueData: "Poderosa Terminal Shortcut"; Flags: uninsdeletevalue; Tasks: associate
Root: HKCR; Subkey: "Poderosa Terminal Shortcut"; ValueType: string; ValueName: ""; ValueData: "Terminal Emulator <Poderosa>"; Flags: uninsdeletekey; Tasks: associate
Root: HKCR; Subkey: "Poderosa Terminal Shortcut\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\Poderosa.exe,0"; Tasks: associate
Root: HKCR; Subkey: "Poderosa Terminal Shortcut\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Poderosa.exe"" ""%1"""; Tasks: associate

[Run]
Filename: "{app}\Poderosa.exe"; Description: "{cm:LaunchProgram,Poderosa}"; Flags: nowait postinstall skipifsilent

[Code]
(* We cannot use ExpandConstant('{cm:...}') during the uninstallation task... *)
function MyConfirmMessageOfDelete: String;
begin
  if ActiveLanguage = 'ja' then
    Result := ExpandConstant('フォルダ'+Chr(13)+Chr(13)+'{app}'+Chr(13)+Chr(13)+'を削除します。よろしいですか？')
  else
    Result := ExpandConstant('The folder:'+Chr(13)+Chr(13)+'{app}'+Chr(13)+Chr(13)+'still exists. Would you like the folder to be deleted?');
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  case CurUninstallStep of
    usPostUninstall:
      if DirExists(ExpandConstant('{app}')) then
        if MsgBox(MyConfirmMessageOfDelete, mbConfirmation, MB_YESNO or MB_DEFBUTTON2) = IDYES then
          DelTree(ExpandConstant('{app}'), True, True, True);
  end;
end;

function InitializeSetup: Boolean;
var
  PoderosaInstalledVersion: String;
begin
  Result := true;
  If RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Poderosa_is1', 'DisplayName', PoderosaInstalledVersion) then
    If Copy(PoderosaInstalledVersion, 1, 19) = 'Poderosa 3.0.0 Beta' Then
      begin
        if ActiveLanguage = 'ja' then
          MsgBox('Poderosa 3.0.0 Beta版に対する上書きインストールは行えません。'+Chr(13)+
                 'お手数ですが、次の手順でインストールをお願いいたします。'+Chr(13)+Chr(13)+
                 '１．（必要なら）Poderosaの設定ファイルをバックアップする。'+Chr(13)+
                 '２．Poderosa 3.0.0 Beta版をアンインストールする。'+Chr(13)+
                 '３．Poderosaを新規インストールする。'+Chr(13)+
                 '４．（必要なら）バックアップしておいた設定ファイルをリストアする。', mbCriticalError, MB_OK)
        else
          MsgBox('To install Poderosa 3.0.0, please uninstall the beta version with the following instructions.'+Chr(13)+
                 '1. Back up configuration files if necessary.'+Chr(13)+
                 '2. Uninstall Poderosa 3.0.0 Beta.'+Chr(13)+
                 '3. Install this 3.0.0 release.'+Chr(13)+
                 '4. If you got the configuration file in step 1, restore it.', mbCriticalError, MB_OK);
        Result := false;
      end;
end;

